.template 0
###############################################################################
# Copyright (c) 2011-2014 libbitcoin developers (see COPYING).
#
# GSL libbitcoin utilities in the 'global' scope.
#
# This is a code generator built using the iMatix GSL code generation
# language. See https://github.com/imatix/gsl for details.
###############################################################################
# Functions
###############################################################################

# General utilities.

function global.is_empty(value)
    return ((my.value ? "") = "")
endfunction

function global.is_true(value)
    return ((my.value ? "false") = "true")
endfunction

function global.starts_with(text, prefix)
    return string.prefixed(my.text, my.prefix)
endfunction

function global.trim_left(text)
    define extracted = ""
    regexp.match("^ +(.*)$", my.text, extracted)
    return extracted ? ""
endfunction

function global.trim_right(text)
    return string.trim(my.text)
endfunction

function global.trim_both(text)
    return trim_left(string.trim(my.text))
endfunction

function make_version(major, minor, build, delimit)
    return "$(my.major:)$(my.delimit:)$(my.minor:)$(my.delimit:)$(my.build:)"
endfunction

# Configure and pkgconfig utilities.

function global.is_boost(dependency)
    define node = is_boost.dependency
    return node.name = "boost"
endfunction

function global.is_boost_lib(dependency)
    define node = is_boost_lib.dependency
    return starts_with(node.name, "boost_")
endfunction

function global.is_boost_dependency(dependency)
    define node = is_boost_dependency.dependency
    return is_boost(node) | is_boost_lib(node)
endfunction

function global.is_package(dependency)
    define node = is_package.dependency
    return !is_empty(node.version) & !is_boost_dependency(node)
endfunction

function global.is_unpackaged_lib(dependency)
    define node = is_unpackaged_lib.dependency
    return !is_empty(node.function) & !is_boost_dependency(node)
endfunction

###############################################################################
# Macros
###############################################################################
.endtemplate
.template 1
.
.macro global.notify(filename)
.   echo "Generating $(my.filename:)..."
.endmacro
.
.macro global.heading1(text)

# $(my.text:)
#==============================================================================
.endmacro heading1
.
.macro global.heading2(text)
# $(my.text:)
#------------------------------------------------------------------------------
.endmacro heading2
.
.macro global.copyleft(name)
###############################################################################
# Copyright (c) 2011-2014 $(my.name:) developers (see COPYING).
#
#         GENERATED SOURCE CODE, DO NOT EDIT EXCEPT EXPERIMENTALLY
#
###############################################################################
.endmacro copyleft
.endtemplate