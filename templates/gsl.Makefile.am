.template 0
###############################################################################
# Copyright (c) 2014-2015 libbitcoin developers (see COPYING).
#
# GSL generate libbitcoin Makefile.am.
#
# This is a code generator built using the iMatix GSL code generation
# language. See https://github.com/imatix/gsl for details.
###############################################################################
# Functions
###############################################################################

function is_multiple_first_optional(table, row)
    define my.table = is_multiple_first_optional.table
    define my.row = is_multiple_first_optional.row
    define my.name = my.row.name
    define my.value = my.row.value
    return !is_empty(my.value) &\
        (table_ordinal(my.table, my.name, 0) = my.value) &\
        (table_count(my.table, my.name) > 1)
endfunction

function render_variables(table)
    define my.table = render_variables.table
    for my.table.row as _row
        write_line("$(_row.name) =")
    endfor
endfunction

function get_initial_variables(build, variables, mapper)
    define my.build = get_initial_variables.build
    define my.variables = get_initial_variables.variables
    define my.mapper = get_initial_variables.mapper
    new table as _table
        for my.build.product as _product
            define my.option = _product.option ? ""
            define my.variable = get_automake_variable(_product)
            table_add(_table, my.variable, my.option)
            if (is_test(_product))
                table_add(_table, "TESTS", my.option)
            endif
            if (is_clean_files(_product))
                table_add(_table, "CLEANFILES", my.option)
            endif
        endfor
        for _table.row as _row where is_multiple_first_optional(_table, _row)
            map_add(my.mapper, _row.name)
            table_add(my.variables, _row.name, "")
        endfor
    endnew _table
endfunction

# Get an = or += depending on whether it's the first or later use.
function get_assignment_operator(mapper, variable)
    define my.found = map_exists(my.mapper, my.variable)
    map_add(my.mapper, my.variable)
    return my.found ?? "+=" ? "="
endfunction

# Map the prefix to the primary symbol (this is a simplified mapping).
function get_primary(product)
    define my.product = get_primary.product
    if (is_data(my.product))
        return "DATA"
    elsif (is_headers(my.product))
        return "HEADERS"
    elsif (is_java(my.product))
        return "JAVA"
    elsif (is_info(my.product))
        return "TEXINFOS"
    elsif (is_library(my.product))
        return "LTLIBRARIES"
    elsif (is_manual(my.product))
        return "MANS"
    elsif (is_program(my.product))
        return "PROGRAMS"
    elsif (is_python(my.product))
        return "PYTHON"
    elsif (is_script(my.product))
        return "SCRIPTS"
    endif
    abort "Unknown automake primary for prefix: '$(my.product.prefix)'"
endfunction

function get_qualified_prefix(product)
    define my.product = get_qualified_prefix.product
    require(my.product, "product", "prefix")
    define my.prefix = my.product.prefix
    define my.install = ""
    define my.distribute = ""

    # dist and nodist qualifications are mutually-exclusive and allow no check.
    if (!is_check(my.product))
        if (is_no_distribute(my.product))
            my.distribute = "nodist"
        elsif (is_distribute(my.product))
            my.distribute = "dist"
        endif
    endif
    
    # check and noinst qualifications are mutually-exclusive and use no prefix.
    if (is_check(my.product) | is_no_install(my.product))
        my.prefix = ""
        if (is_check(my.product))
            my.install = "check"
        elsif (is_no_install(my.product))
            my.install = "noinst"
        endif
    endif
    
    # join and return the non-empty parts
    return join(join(my.distribute, my.install, "_"), my.prefix, "_")
endfunction

# Return true if the path extension is that of a c/c++ header file.
function is_cpp_header_file(path)
    return ends_with(my.path, ".hpp") | ends_with(my.path, ".h") |\
        ends_with(my.path, ".ipp")
endfunction

# Return true if the path extension is that of a c/c++ source file.
function is_cpp_source_file(path)
    return ends_with(my.path, ".cpp") | ends_with(my.path, ".c")
endfunction

# Does the file extension conform to the required prefix pattern?
function is_product_file(product, path)
    define my.product = is_product_file.product
    require(my.product, "product", "prefix")

    # doc expects no type and bash_completion and manual have no extension.
    if (is_data(my.product))
        if (match_prefix(my.product, "bash_completion"))
            return ends_with(my.path, "")
        elsif (match_prefix(my.product, "doc"))
            return ends_with(my.path, "")
        elsif (match_prefix(my.product, "pkgconfig"))
            return ends_with(my.path, ".pc")
        elsif (match_prefix(my.product, "sysconf"))
            return ends_with(my.path, ".cfg")
        endif
    elsif (is_cpp(my.product))
        # Headers are included for dist, Automake ignores otherwise.
        return is_cpp_source_file(my.path) | is_cpp_header_file(my.path)
    elsif (is_headers(my.product))
        return is_cpp_header_file(my.path)
    elsif (is_info(my.product))
        return ends_with(my.path, ".texi")
    elsif (is_java(my.product))
        return ends_with(my.path, ".java")
    elsif (is_manual(my.product))
        return ends_with(my.path, "")
    elsif (is_python(my.product))
        return ends_with(my.path, ".py")
    elsif (is_script(my.product))
        return ends_with(my.path, ".sh")
    endif
    abort "Unknown product type for prefix: '$(my.product.prefix)'"
endfunction

# Get the target attribute, defaulting to [prefix]dir, bracketed as ${target}.
function get_target(product)
    require(my.product, "product", "prefix")
    define my.prefix = my.product.prefix
    
    # Workaround for failure of automake to properly handle noinst_JAVA.
    if (is_java(my.product) & is_no_install(my.product))
        my.prefix = "build"
    endif
    define my.target = "$(my.prefix)dir"
    return "${$(my.target)}"
endfunction

# Get the full install location.
function get_install(product, container)
    define my.product = get_install.product
    if (is_empty(my.container))
        my.container = my.product.container ?
    endif
    if (is_empty(my.container))
        return ""
    else
        my.container = "$(my.container)"
    endif
    define my.target = get_target(my.product)
    return "$(my.target)/$(my.container)"
endfunction

# Normalize the library name (as "[lib]name[.la]").
function get_library_name(library)
    define my.library = get_library_name.library
    define my.prefix = is_true(my.library.unprefixed) ?? "" ? "lib"
    return "$(my.prefix)$(my.library.name).la"
endfunction

# Normalize the product name.
function get_product_name(product)
    define my.product = get_product_name.product
    if (is_program(my.product))
        return my.product.name
    elsif (is_library(my.product))
        return get_library_name(my.product)
    endif
    return ""
endfunction

# Create the product path.
function get_product_path(product)
    define my.product = get_product_path.product
    return join(my.product.path, get_product_name(my.product))
endfunction

# Get the command to execute the test runner. If there is a runner element the
# test runner is a generated script ${sourcedir}/[product.name]_runner.sh.
# Otherwise the runner is ${sourcedir}/[product.path]/[product.name].
function get_test_path(product)
    define my.product = get_test_path.product
    define my.name = get_product_name(my.product)
    if (defined(my.product->runner))
        return "$(my.name)_runner.sh"
    endif
    return join(my.product.path, my.name)
endfunction

# Get a description for comments.
function get_product_description(product, container)
    define my.product = get_product_description.product
    define my.target = get_target(my.product)
    if (is_cpp(my.product))
        define my.path = get_product_path(my.product)
        if (is_check(my.product) | is_no_install(my.product))
            return "local: $(my.path)"
        endif
        return "$(my.path) => $(my.target)"
    elsif (is_jar(my.product))
        if (is_no_install(my.product))
            return "*.class => *.jar (local), requires ax_java_devel.m4"
        endif
        return "*.class => *.jar => $(my.target), requires ax_java_devel.m4"
    elsif (is_java(my.product))
        if (is_no_install(my.product))
            return "*.java => *.class (local), requires ax_java_devel.m4"
        endif
        return "*.java => *.class => $(my.target), requires ax_java_devel.m4"
    endif
    define my.install = get_install(my.product)
    if (is_empty(my.install))
        my.install = my.target
    endif
    return "files => $(my.install)"
endfunction

# Get automake tag of the form: qualified-prefix[_container]
function get_tag(product, container)
    define my.product = get_tag.product
    if (is_empty(my.container))
        my.container = my.product.container ? ""
    endif
    if (!is_empty(my.container))
        my.container = "_$(my.container:c)"
    endif
    define my.qualified_prefix = get_qualified_prefix(my.product)
    return "$(my.qualified_prefix)$(my.container)"
endfunction

# Get the automake library add output variable type.
function get_library_add(product)
    define my.product = get_library_add.product
    if (is_library(my.product))
        return "LIBADD"
    elsif (is_program(my.product))
        return "LDADD"
    endif
    abort "Unknown library type: '$(my.product.prefix)'"
endfunction

# Open and validate a directory.
function open_directory(folder)
    define my.directory = directory.open(my.folder, error_text) ?
    if (defined(my.directory))
        return my.directory
    endif
    abort "Directory enumeration failure: $(error_text)"
endfunction

# Test a directory and return the normalized path.
function normalize_directory(folder)
    define my.directory = open_directory(my.folder)
    return my.directory.path
endfunction

function get_relative_path(full_path, trim)
    define my.relative_path = difference(my.full_path, my.trim)
    return " \\\n    $(my.relative_path)"
endfunction

# Write the file names in the directory to a formatted string.
function get_files(folder, trim, product)
    define my.product = get_files.product
    my.files = ""
    define my.directory = open_directory(my.folder)
    for my.directory.file as _file by _file.name\
         where is_product_file(my.product, _file.name)
        define my.full_path = "$(_file.path)$(_file.name)"
        my.files += get_relative_path(my.full_path, my.trim)
    endfor
    return my.files
endfunction

# Recurse directory tree, returning the formatted paths in flat list.
function get_all_files(folder, trim, product)
    define my.product = get_all_files.product
    define my.files = get_files(my.folder, my.trim, my.product)
    define my.directory = open_directory(my.folder)
    for my.directory.directory as _directory by _directory.name
        define my.subfolder = "$(_directory.path)$(_directory.name)"
        my.files += get_all_files(my.subfolder, my.trim, my.product)
    endfor
    return my.files
endfunction

# Recurse the directory tree, rendering all files.
function render_all_variables(folder, base_trim, path_trim, product, mapper)
    define my.mapper = render_all_variables.mapper
    define my.product = render_all_variables.product
    define my.directory = open_directory(my.folder)
    for my.directory.directory as _directory by _directory.name
        define my.subfolder = "$(_directory.path)$(_directory.name)"
        render_files_variables(my.subfolder, my.base_trim, my.path_trim,\
            my.product, my.mapper)
    endfor
endfunction

# Helper function for building a container segment.
function construct_segment(product, folder, trim)
    define my.product = construct_segment.product
    define my.segment = difference(my.folder, my.trim)
    define my.container = my.product.container ? ""
    return join(my.container, my.segment)
endfunction

# Recurse directory tree, rendering outputs with files for each folder.
function render_files_variables(folder, base_trim, path_trim, product, mapper)
    define my.product = render_files_variables.product
    define my.mapper = render_files_variables.mapper
    require(my.product, "product", "prefix")
    define my.files = get_files(my.folder, my.base_trim, my.product)
    if (!is_empty(my.files))
        define my.trim = my.base_trim + my.path_trim + 1
        define my.segment = construct_segment(my.product, my.folder, my.trim)
        define my.tag = get_tag(my.product, my.segment)
        define my.install = get_install(my.product, my.segment)
        define my.variable = get_automake_variable(my.product, my.segment)
        # Don't write a tautology, such as: installdir = ${installdir}
        if (!is_empty(my.install))
            write_line("$(my.tag)dir = $(my.install)")
        endif
        define my.operator = get_assignment_operator(my.mapper, my.variable)
        write_line("$(my.variable) $(my.operator)$(my.files)\n")
    endif
    render_all_variables(my.folder, my.base_trim, my.path_trim, my.product,\
        my.mapper)
endfunction

# Format the flag either as a literal flag or a dereferenced variable.
function get_flag(flag)
    define my.flag = get_flag.flag
    if (defined(my.flag.value))
        return "-$(my.flag.value)"
    elsif (defined(my.flag.define))
         return "-D$(my.flag.define:upper)=\\\"${$(my.flag.define)}\\\""
    elsif (defined(my.flag.substitute))
        return "${$(my.flag.substitute)}"
    endif
    abort "Required attribute missing: flag.value|define|substitute"
endfunction

# Get the product with the given library name.
function get_library_product(name, build)
    define my.build = get_library_product.build
    return my.build->product((product.name ? "") = my.name)
endfunction

function get_chase_product(library, build)
    define my.library = get_chase_product.library
    define my.build = get_chase_product.build
    return my.build->product(is_library(product) &\
        (product.name = my.library.name))
endfunction

function chase_libraries(product, build)
    define my.product = chase_libraries.product
    define my.build = chase_libraries.build
    define my.cursor = set_cursor_end(my.product)
    for my.product.library as _library
        define my.chased_product = get_chase_product(_library, my.build)?
        if (defined(my.chased_product))
            for my.chased_product.library as _chased_library
                copy _chased_library before my.cursor
            endfor
        endif
    endfor
    clear_cursor(my.cursor)
endfunction

function get_tests_variable(product, mapper)
    define my.product = get_tests_variable.product
    define my.mapper = get_tests_variable.mapper
    define my.path = get_test_path(my.product)
    define my.variable = "TESTS"
    define my.operator = get_assignment_operator(my.mapper, my.variable)
    return "$(my.variable) $(my.operator) $(my.path)"
endfunction

function get_binaries_variable(product, mapper)
    define my.product = get_binaries_variable.product
    define my.mapper = get_binaries_variable.mapper
    define my.path = get_product_path(my.product)
    define my.variable = get_automake_variable(my.product)
    define my.operator = get_assignment_operator(my.mapper, my.variable)
    return "$(my.variable) $(my.operator) $(my.path)"
endfunction

function initialize_cpp_variables(variables)
    define my.variables = initialize_cpp_variables.variables
    my.variables.libs = ""
    my.variables.ld_flags = ""
    my.variables.ccc_flags = ""
    my.variables.cxx_flags = ""
    my.variables.cpp_flags = ""
    my.variables.sources = ""
endfunction

function write_cpp_variables(variables, product)
    define my.product = write_cpp_variables.product
    define my.variables = write_cpp_variables.variables
    define my.cpath = "$(get_product_path(my.product):c)"
    if (defined(my.variables.tests))
        write_line("$(my.variables.tests)\n")
    endif
    if (defined(my.variables.binaries))
        write_line("$(my.variables.binaries)")
    endif
    if (!is_empty(my.variables.ccc_flags))
        write_line("$(my.cpath)_CFLAGS =$(my.variables.ccc_flags)")
    endif
    if (!is_empty(my.variables.cxx_flags))
        write_line("$(my.cpath)_CXXFLAGS =$(my.variables.cxx_flags)")
    endif
    if (!is_empty(my.variables.cpp_flags))
        write_line("$(my.cpath)_CPPFLAGS =$(my.variables.cpp_flags)")
    endif
    if (!is_empty(my.variables.ld_flags))
        write_line("$(my.cpath)_LDFLAGS =$(my.variables.ld_flags)")
    endif
    if (!is_empty(my.variables.libs))
        define my.add = get_library_add(my.product)
        write_line("$(my.cpath)_$(my.add) =$(my.variables.libs)")
    endif
    if (!is_empty(my.variables.sources))
        write_line("$(my.cpath)_SOURCES =$(my.variables.sources)")
    endif
    write_line()
endfunction

function define_cpp_flag_variable(variables, flag)
    define my.variables = define_cpp_flag_variable.variables
    define my.flag = define_cpp_flag_variable.flag
    define my.flag_value = " $(get_flag(my.flag))"
    if (is_ccc_flag(my.flag))
        my.variables.ccc_flags += my.flag_value
    elsif (is_cxx_flag(my.flag))
        my.variables.cxx_flags += my.flag_value
    elsif (is_cpp_flag(my.flag))
        my.variables.cpp_flags += my.flag_value
    else
        define my.context = my.flag.context ? ""
        abort "Invalid flag context: '$(my.context)'."
    endif
endfunction

function define_cpp_lib_variables(variables, library, build, configure)
    define my.variables = define_cpp_lib_variables.variables
    define my.library = define_cpp_lib_variables.library
    define my.build = define_cpp_lib_variables.build
    define my.configure = define_cpp_lib_variables.configure
    require(my.library, "library", "name")
    define my.built = get_library_product(my.library.name, my.build)?
    if (defined(my.built))
        define my.library_name = get_library_name(my.built)
        my.library_path = join(my.built.path, my.library_name)
        my.variables.libs += " $(my.library_path)"
    else
        # pkg-config is problematic in that it mixes LDFLAGS into LIBS
        # and all flags into CFLAGS. We rewrite its CFLAGS as CPPFLAGS.
        define my.library_cname = "$(my.library.name:c)"
        define my.is_language = is_java_dependency(my.library) |\
            is_python_dependency(my.library)
        define my.is_boost = is_boost_dependency(my.library)
        define my.is_boost_lib = is_boost_lib_dependency(my.library)
        define my.is_packaged = is_packaged_library(my.library.name, my.configure)
        if (!my.is_boost & !my.is_language)
            my.variables.libs += " ${$(my.library_cname)_LIBS}"
        endif
        if (!my.is_boost_lib)
            my.variables.cpp_flags += " ${$(my.library_cname)_CPPFLAGS}"
        endif
        if (!my.is_packaged & !my.is_boost & !my.is_boost_lib & !my.is_language)
            my.variables.ccc_flags += " ${$(my.library_cname)_CFLAGS}"
        endif
        if (!my.is_packaged & !my.is_boost_lib & !my.is_language)
            my.variables.ld_flags += " ${$(my.library_cname)_LDFLAGS}"
        endif
        #my.variables.cxx_flags += " ${$(my.library_cname)_CXXFLAGS}"
    endif
endfunction

# Render c/c++ specific option variables.
function render_cpp_variables(product, root, repository, mapper)
    define my.product = render_cpp_variables.product
    define my.repository = render_cpp_variables.repository
    
    if (is_cpp(my.product))
        define my.build = my.repository->build
        define my.configure = my.repository->configure
        define my.root_length = string.length(my.root)
        new variables as _variables
        initialize_cpp_variables(_variables)
        chase_libraries(my.product, my.build)
        
        # Define _PROGRAMS|_[LT]LIBRARIES variables.
        _variables.binaries = get_binaries_variable(my.product, my.mapper)

        # Define TESTS variable.
        if (is_test(my.product))
            _variables.tests = get_tests_variable(my.product, my.mapper)
        endif
        
        # Define _LDFLAGS from an override of Libtool default naming.
        if (is_true(my.product.unprefixed))
            _variables.ld_flags += " -module"
        endif

        # Define _CPPFLAGS values from input.
        for my.product.headers as _headers
            require(_headers, "headers", "path")
            _variables.cpp_flags += " -I${srcdir}/$(_headers.path)"
        endfor
        
        # Define _C??FLAGS values from input.
        for my.product.flag as _flag
            define_cpp_flag_variable(_variables, _flag)
        endfor
        
        # Define _LIBS|_LDFLAGS|C??FLAGS option variables from libraries.
        for my.product.library as _library
            define_cpp_lib_variables(_variables, _library, my.build, my.configure)
        endfor
        
        # Define _SOURCES option variable from sources elements.
        for my.product.sources as _sources
            require(_sources, "sources", "path")
            define my.folder = "$(my.root)$(_sources.path)"
            define _variables.sources += get_all_files(my.folder,\
                my.root_length, my.product)
        endfor
        
        write_cpp_variables(_variables, my.product)
        endnew
    endif
endfunction

# Render java specific variables: JAVA.
function render_java_variables(product, root, repository, mapper)
    define my.product = render_java_variables.product
    define my.repository = render_java_variables.repository
    if (is_java(my.product))
        define my.operator = get_assignment_operator(my.mapper, "CLEANFILES")
        write_line("CLEANFILES $(my.operator) ${java_builddir}/*.class\n")
        write_line("distclean-local:")
        write_line("\t-rm -rf ${java_builddir} ${jar_builddir}\n")
    endif
endfunction

function get_java_stamp_variable(product)
    define my.product = get_java_stamp_variable.product
    define my.container = my.product.container?
    define my.prefix = is_empty(my.container) ?? "javaexec" ? ""
    define my.install = is_no_install(my.product) ?? "noinst" ? my.prefix
    return join(my.install, "$(my.container:c)", "_")
endfunction

function get_java_stamp(repository)
    define my.repository = get_java_stamp.repository
    define my.build = my.repository->build?
    if (defined(my.build))
        define my.java_product = my.build->product(prefix = "javaexec")?
        if (defined(my.java_product))
            define my.prefix = get_java_stamp_variable(my.java_product)
            return "class$(my.prefix).stamp"
        endif
    endif
    abort "The build is missing a required java product."
endfunction

function get_automake_variable(product, segment)
    define my.product = get_automake_variable.product
    define my.tag = get_tag(my.product, my.segment)
    return join(my.tag, get_primary(my.product), "_")
endfunction

# Render java specific variables: jar_DATA.
function render_jar_variables(product, root, repository, mapper)
    define my.product = render_jar_variables.product
    define my.repository = render_jar_variables.repository
    if (is_jar(my.product))
        define my.stamp = get_java_stamp(my.repository)
        define my.variable = get_automake_variable(my.product)
        define my.operator = get_assignment_operator(my.mapper, "CLEANFILES")
        write_line("${$(my.variable)}: $(my.stamp)")
        write_line("\t${JAR} cf ${JARFLAGS} ${$(my.variable)} -C ${java_builddir} .\n")
        write_line("CLEANFILES $(my.operator) ${jar_builddir}/*.jar\n")
    endif
endfunction

# Render python specific variables: PYTHON.
function render_python_variables(product, root, repository, mapper)
    define my.product = render_python_variables.product
    define my.repository = render_python_variables.repository
    if (is_python(my.product))
        # TODO: add python features.
    endif
endfunction

function get_source_directory(product)
    define my.product = get_source_directory.product
    if (is_jar(my.product))
        return "${jar_builddir}"
    endif
    return ""
endfunction

function render_file_variables(product, mapper)
    define my.product = render_file_variables.product
    define my.file = ""
    define my.source = get_source_directory(my.product)
    for my.product.file as _file where !is_empty(_file.path) by _file.path
        define my.path = join(my.source, _file.path)
        my.file += " \\\n    $(my.path)"
    endfor
    if (!is_empty(my.file))
        define my.tag = get_tag(my.product)
        define my.variable = get_automake_variable(my.product)
        define my.install = get_install(my.product, my.container)
        
        # Don't write a tautology, such as: installdir = ${installdir}.
        if (!is_empty(my.install))
            write_line("$(my.tag)dir = $(my.install)")
        endif
        define my.operator = get_assignment_operator(my.mapper, my.variable)
        write_line("$(my.variable) $(my.operator)$(my.file)\n")
    endif
endfunction

function render_product_variables(product, root, repository, mapper)
    define my.product = render_product_variables.product
    define my.repository = render_product_variables.repository
    
    # File elements are enumerated in metadata, just render them.
    render_file_variables(my.product, my.mapper)

    for my.product.files as _files where !is_empty(_files.path)
        define my.folder = "$(my.root)$(_files.path)"
        define my.root_length = string.length(my.root)
        define my.path_length = string.length(_files.path)
        
        # Files elements are enumerated by directory and mapped to targets.
        render_files_variables(my.folder, my.root_length, my.path_length,\
            my.product, my.mapper)
    endfor
    
    render_cpp_variables(my.product, my.root, my.repository, my.mapper)
    render_java_variables(my.product, my.root, my.repository, my.mapper)
    render_jar_variables(my.product, my.root, my.repository, my.mapper)
    render_python_variables(my.product, my.root, my.repository, my.mapper)
endfunction
    
function render_product(product, root, repository, mapper)
    define my.product = render_product.product
    define my.repository = render_product.repository
    define my.configure = my.repository->configure
    define my.option = find_option_symbol(my.product, my.configure)?
    if (defined(my.option))
        write_line("if $(my.option:upper)\n")
    endif
    render_product_variables(my.product, my.root, my.repository, my.mapper)
    if (defined(my.option))
        write_line("endif $(my.option:upper)\n")
    endif
endfunction

function initialize_automake()
    heading2("Look for macros in the m4 subdirectory.")
    write_line("ACLOCAL_AMFLAGS = -I m4\n")
endfunction

# For each option, write all paths to the option-named target.
function render_make_targets(targets)
    define my.targets = render_make_targets.targets
    define my.last_row_name = ""
    for my.targets.row as _outer by _outer.name
        if (_outer.name = my.last_row_name)
            next _outer
        endif
        define my.target = ""
        my.last_row_name = _outer.name
        for my.targets.row as _inner where (_inner.name = _outer.name)
            my.target += " \\\n    $(_inner.value)"
        endfor
        heading2("make target: $(_outer.name)")
        write_line("target_$(_outer.name) =$(my.target)\n")
        write_line("$(_outer.name): ${target_$(_outer.name)}\n")
    endfor
endfunction

# For each non-test product that is not a test, accumulate the product's 
# option (name) and path (value) in the targets table. 
function define_make_targets(build, targets)
    define my.build = define_make_targets.build
    define my.targets = define_make_targets.targets
    for my.build.product as _product where is_custom_target(_product)
        define my.path = get_product_path(_product)
        if (!is_empty(my.path))
            table_add(my.targets, _product.option, my.path)
        endif
    endfor
endfunction

###############################################################################
# Generation
###############################################################################

function generate_automake()
for generate.repository as _repository by _repository.name\
    where (defined(_repository->build))

    require(_repository, "repository", "name")
    define my.out_file = "$(_repository.name)/Makefile.am"
    notify(my.out_file)
    output(my.out_file)

    copyleft(_repository.name)
    define my.build = _repository->build
    define my.base = "$(global.root)/$(_repository.name)/"
    define my.root = normalize_directory(my.base)
    
    heading1("Automake settings.")
    initialize_automake()
    
    new mapper as _mapper
        new variables as _variables
            get_initial_variables(my.build, _variables, _mapper)
            if (!table_empty(_variables))
                heading1("Conditional variables.")
                render_variables(_variables)
            endif
        endnew _variables
        
        heading1("Distribution, build and install products.")
        for my.build.product as _product
            heading2(get_product_description(_product))
            render_product(_product, my.root, _repository, _mapper)
        endfor
    endnew _mapper
    
    new targets as _targets
        define_make_targets(my.build, _targets)
        if (!table_empty(_targets))
            heading1("Custom make targets.")
            render_make_targets(_targets)
        endif
   endnew _targets
       
    close
endfor _repository
endfunction # generate_automake
.endtemplate